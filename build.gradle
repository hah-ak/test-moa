plugins {
    id 'org.springframework.boot' version '3.2.7' apply false
    id 'io.spring.dependency-management' version '1.1.0' apply false
    id "io.freefair.lombok" version "8.6" apply false
}
ext {
    set("springCloudVersion", "2023.0.2")
    defaultForbiddenStrings = ["LocalDateTime"]
    checkForbiddenCode = { sourceDir, forbiddenStrings ->
        sourceDir.eachFileRecurse { file ->
            if (file.name.endsWith(".java")) {
                def fileContent = file.text
                forbiddenStrings.addAll(ext.defaultForbiddenStrings)
                forbiddenStrings.each { forbidden ->
                    if (fileContent.contains(forbidden)) {
                        throw new GradleException("forbidden text exist")
                    }
                }
            }
        }
    }
}

group = 'my.application'
version = '1.0'

allprojects {
    apply plugin: 'java'
}

subprojects {
    sourceCompatibility = '17'

    apply plugin: 'org.springframework.boot' // 여기에 java plugin이 포함되어 있으나, 루트 프로젝트에서 사용을 위해 java플러그인 따로 선언
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        implementation platform("io.opentelemetry:opentelemetry-bom:1.41.0")
        implementation 'io.opentelemetry:opentelemetry-api'
        implementation 'io.opentelemetry:opentelemetry-sdk'
        implementation 'io.opentelemetry:opentelemetry-exporter-logging'
        implementation 'io.opentelemetry.semconv:opentelemetry-semconv:1.27.0-alpha'
        implementation 'io.opentelemetry:opentelemetry-sdk-extension-autoconfigure'
    }
}
